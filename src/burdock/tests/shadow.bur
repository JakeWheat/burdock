

check "shadow":

  # this test will need to be adjusted when lexical bindings/ shadow is
  # tested statically

  block:
    a = 3
    a is 3
    shadow a = 4
    a is 4

    block:
      b = 3
      b = 4
    end raises 'declaration of name b conflicts with earlier declaration'

    v = 3
    v is 3
    var shadow v = 4
    v is 4
  end

  block:
    u = 3
    var u = 4
  end raises 'declaration of name u conflicts with earlier declaration'

  var v1 = 3
  v1 is 3
  shadow v1 = 4
  v1 is 4

  block:
    var u1 = 3
    u1 = 4
  end raises 'declaration of name u1 conflicts with earlier declaration'

#|

do empty = x, where x is empty list
and shadow empty = x

|#

  block:
    l = empty
    empty = l
    empty
  end raises "declaration of name empty conflicts with earlier declaration"

  block:
    l = empty
    shadow empty = l
    empty
  end is empty

  block:
    x12 = 1
    x = {1;2}
    cases x:
      | _ as x12 => x12
    end
  end raises "declaration of name x12 conflicts with earlier declaration"

  block:
    cc = 1
    x = {1;2}
    cases x:
      | _ as shadow cc => cc
    end
  end is {1;2}

  block:
    fun f():
      nothing
    end

    f() is nothing

    fun shadow f():
      1
    end

    f() is 1


  end

end
